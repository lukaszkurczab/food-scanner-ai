// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    classpath 'com.google.gms:google-services:4.4.1'
    classpath('com.android.tools.build:gradle')
    classpath('com.facebook.react:react-native-gradle-plugin')
    classpath('org.jetbrains.kotlin:kotlin-gradle-plugin')
  }
}

def reactNativeAndroidDir = new File(
  providers.exec {
    workingDir(rootDir)
    commandLine("node", "--print", "require.resolve('react-native/package.json')")
  }.standardOutput.asText.get().trim(),
  "../android"
)

allprojects {
  repositories {
    maven {
      // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
      url(reactNativeAndroidDir)
    }

    google()
    mavenCentral()
    maven { url 'https://www.jitpack.io' }
  }
}

apply plugin: "expo-root-project"
apply plugin: "com.facebook.react.rootproject"

// Disable lint checks across all subprojects to avoid OOM during release builds
subprojects { subproject ->
  def configureLint = {
    if (subproject.hasProperty('android')) {
      // AGP < 8
      try {
        subproject.android.lintOptions?.abortOnError = false
        subproject.android.lintOptions?.checkReleaseBuilds = false
      } catch (ignored) {}
      // AGP 8+
      try {
        subproject.android.lint { abortOnError = false; checkReleaseBuilds = false }
      } catch (ignored) {}
    }
  }
  subproject.plugins.withId('com.android.application') { configureLint() }
  subproject.plugins.withId('com.android.library') { configureLint() }
}
